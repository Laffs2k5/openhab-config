// Imports
import java.util.HashMap                        //
import org.eclipse.xtext.xbase.lib.Functions    // Needed for use of lambda
import java.util.concurrent.locks.ReentrantLock // Needed to block rules from triggering in paralell
import java.util.concurrent.TimeUnit            //

// Constants
val String LOGGER_NAME = "cmpterRoom"
val String IR_MAPFILE = "broadlink.map"

// Scene control commands
val int CONTROL_SCENE_NOCHANGE   = -1
val int CONTROL_SCENE_ALLOFF     = 0  // Turn off receiver + TV
val int CONTROL_SCENE_CHROMECAST = 1  // Turn on receiver + TV and set to chromecast input
val int CONTROL_SCENE_COMPUTER   = 2  // Turn on receiver + TV and set to computer input
val int CONTROL_SCENE_NINTENDO   = 3  // Turn on receiver + TV and set to nintendo input

// Common control commands
val String CONTROL_NOCHANGE = "CONTROL_NOCHANGE"  // do nothing

// TV control commands to commands definition (ir command name and time the ir command takes to transmit)
val HashMap<String, HashMap<String,String>> g_tvIrCommandsDefMap = newHashMap(
     "CONTROL_TV_VOL_MINUS_40"->  (newHashMap("irCommand" ->  "SONY_W5500_VOLUME_DOWN_X80",     "irTransmitTimeMs" ->  "3500"))
    ,"CONTROL_TV_VOL_MINUS"   ->  (newHashMap("irCommand" ->  "SONY_W5500_VOLUME_DOWN_X3",      "irTransmitTimeMs" ->  "100"))
    ,"CONTROL_TV_VOL_PLUS"    ->  (newHashMap("irCommand" ->  "SONY_W5500_VOLUME_UP_X3",        "irTransmitTimeMs" ->  "100"))
    ,"CONTROL_TV_VOL_MUTE"    ->  (newHashMap("irCommand" ->  "SONY_W5500_MUTE_TOGGLE_X5",      "irTransmitTimeMs" ->  "100"))
//    ,"CONTROL_TV_VOL_MINUS70" ->  (newHashMap("irCommand" ->  "SONY_W5500_VOLUME_DOWN_X155",    "irTransmitTimeMs" ->  "7000"))
//    ,"CONTROL_TV_VOL_PLUS70"  ->  (newHashMap("irCommand" ->  "SONY_W5500_VOLUME_UP_X155",      "irTransmitTimeMs" ->  "7000"))
    ,"CONTROL_TV_DISP_THMODE" ->  (newHashMap("irCommand" ->  "SONY_W5500_THEATER_MODE_X5",     "irTransmitTimeMs" ->  "100"))
    ,"CONTROL_TV_DIGIT_1"     ->  (newHashMap("irCommand" ->  "SONY_W5500_DIGIT_1_X5",          "irTransmitTimeMs" ->  "100"))
    ,"CONTROL_TV_INPUT_HDMI1" ->  (newHashMap("irCommand" ->  "SONY_W5500_INPUT_HDMI_1_X5",     "irTransmitTimeMs" ->  "100"))
    ,"CONTROL_TV_INPUT_HDMI3" ->  (newHashMap("irCommand" ->  "SONY_W5500_INPUT_HDMI_3_X5",     "irTransmitTimeMs" ->  "100"))
    ,"CONTROL_TV_INPUT_HDMI4" ->  (newHashMap("irCommand" ->  "SONY_W5500_INPUT_HDMI_4_X5",     "irTransmitTimeMs" ->  "100"))
    ,"CONTROL_TV_POWER_ON"    ->  (newHashMap("irCommand" ->  "SONY_W5500_POWER_ON_X5",         "irTransmitTimeMs" ->  "100"))
    ,"CONTROL_TV_POWER_OFF"   ->  (newHashMap("irCommand" ->  "SONY_W5500_POWER_OFF_X5",        "irTransmitTimeMs" ->  "100"))
)

// Special audio control commands
//  These requires more action than just issuing an IR command
val String CONTROL_AUDIO_POWER_ON = "CONTROL_AUDIO_POWER_ON"
val String CONTROL_AUDIO_POWER_OFF = "CONTROL_AUDIO_POWER_OFF"

// Audio control commands to commands definition (ir command name and time the ir command takes to transmit)
val HashMap<String, HashMap<String,String>> g_audioIrCommandsDefMap = newHashMap(
     "CONTROL_AUDIO_VOL_MINUS_30"       ->  (newHashMap("irCommand" ->  "LOGITECH_Z5500_VOLUMEDOWN_X30",        "irTransmitTimeMs" ->  "4000"))
    ,"CONTROL_AUDIO_VOL_MINUS"          ->  (newHashMap("irCommand" ->  "LOGITECH_Z5500_VOLUMEDOWN_X2",         "irTransmitTimeMs" ->  "100"))
    ,"CONTROL_AUDIO_VOL_PLUS"           ->  (newHashMap("irCommand" ->  "LOGITECH_Z5500_VOLUMEUP_X2",           "irTransmitTimeMs" ->  "100"))
    ,"CONTROL_AUDIO_VOL_DEF_CC"         ->  (newHashMap("irCommand" ->  "LOGITECH_Z5500_VOLUMEUP_X13",          "irTransmitTimeMs" ->  "100"))
    ,"CONTROL_AUDIO_VOL_DEF_COMPUTER"   ->  (newHashMap("irCommand" ->  "LOGITECH_Z5500_VOLUMEUP_X18",          "irTransmitTimeMs" ->  "100"))
//    ,"CONTROL_AUDIO_VOL_ZERO"           ->  (newHashMap("irCommand" ->  "LOGITECH_Z5500_VOLUME_BURST_TO_ZERO",  "irTransmitTimeMs" ->  "7500"))
//    ,"CONTROL_AUDIO_VOL_MAX"            ->  (newHashMap("irCommand" ->  "LOGITECH_Z5500_VOLUME_BURST_TO_MAX",   "irTransmitTimeMs" ->  "7500"))
    ,"CONTROL_AUDIO_VOL_MUTE"           ->  (newHashMap("irCommand" ->  "LOGITECH_Z5500_MUTE",                  "irTransmitTimeMs" ->  "100"))
    ,"CONTROL_AUDIO_INPUT_COAX"         ->  (newHashMap("irCommand" ->  "LOGITECH_Z5500_SOURCE_COAX_X2",        "irTransmitTimeMs" ->  "2000"))
    ,"CONTROL_AUDIO_INPUT_DIRECT"       ->  (newHashMap("irCommand" ->  "LOGITECH_Z5500_SOURCE_DIRECT_X2",      "irTransmitTimeMs" ->  "2000"))
    ,"CONTROL_AUDIO_INPUT_OPTICAL"      ->  (newHashMap("irCommand" ->  "LOGITECH_Z5500_SOURCE_OPTICAL_X2",     "irTransmitTimeMs" ->  "2000"))
    ,"CONTROL_AUDIO_POWER_TOGGLE"       ->  (newHashMap("irCommand" ->  "LOGITECH_Z5500_POWER_X4",              "irTransmitTimeMs" ->  "3000"))
)

// Globals
var ReentrantLock   g_computerRoomLockIrCommands = new ReentrantLock()
var ReentrantLock   g_computerRoomLockTvUpdates = new ReentrantLock()
var ReentrantLock   g_computerRoomLockReceiverUpdates = new ReentrantLock()

// Lambdas
val Functions$Function2<String, GenericItem, Boolean> logReceivedUpdateWithState = [
    loggerName,
    itemUpdated |
    val String LOGGER = loggerName //+ ".logReceivedUpdateWithState"
    logDebug(LOGGER, "Item '{}' received update '{}'",
        itemUpdated.name,
        itemUpdated.state)
    true
]

// Utility function
// Post given ir command code to given IR blaster
// Use given lock to make sure all calls to this function is executed in order
val Functions$Function5<String, String, ReentrantLock, HashMap<String, HashMap<String,String>>, GenericItem, Boolean> transmitIrCommand = [
    loggerName,
    irCommandCode,
    irCommandLock,
    irCommandsMap,
    IrBlasterItem |

    // Constants
    val String LOGGER = loggerName + ".postIrCmdWLock"
    val String IR_DEF_CMD_KEY = "irCommand"
    val String IR_DEF_TRANSMIT_TIME_KEY = "irTransmitTimeMs"

    if (irCommandCode === null) logError(LOGGER, "Missing IR command code!")
    if (irCommandLock === null) logError(LOGGER, "Missing IR command lock!")
    if (irCommandsMap === null) logError(LOGGER, "Missing IR commands map!")
    if (IrBlasterItem === null) logError(LOGGER, "Missing IR blaster item!")

    logDebug(LOGGER, "Going to transmit IR command code '{}'.", irCommandCode)

    // Vars
    val HashMap<String,String> irCommandDef = new HashMap<String,String>(irCommandsMap.get(irCommandCode))
    val String irCommand = irCommandDef.get(IR_DEF_CMD_KEY)
    val Integer irTransmitTime = Integer.parseInt(irCommandDef.get(IR_DEF_TRANSMIT_TIME_KEY))

    logDebug(LOGGER, "IR command code {} corresponds to ir command named {} with transmit time of {} ms", irCommandCode, irCommand, irTransmitTime)

    // Just for debug
    //val String IR_MAPFILE = "broadlink.map"
    //logInfo(LOGGER, "Transforming IR command '{}' using map file '{}' to raw IR command.", irCommand, IR_MAPFILE)
    //val irRawCommand = transform("MAP", IR_MAPFILE, irCommand)
    //logInfo(LOGGER, "Raw IR command is: {}", irRawCommand)

    logDebug(LOGGER, "Waiting for lock...") // makes sure only one IR command is issued at a time
    irCommandLock.lock()
    try {
        logDebug(LOGGER, "Lock aquired.")
        logDebug(LOGGER, "Posting command '{}' to item '{}'", irCommand, IrBlasterItem.name)
        IrBlasterItem.sendCommand(irCommand)
        Thread::sleep(irTransmitTime)
    } catch(Throwable t) {
        logError(LOGGER, "Exception caught while posting ir command. Exception was: {}", t.toString())
    } finally {
        irCommandLock.unlock()
        logDebug(LOGGER, "Lock released.")
    }

    true
]

// Handle cloud items
rule "Computer room scene, cloud item update, chromecast"
when
    Item Cloud_Scene_GF_ComputerRoom_chromecast received update ON
then
    val String LOGGER = LOGGER_NAME + ".sceneCc"
    logReceivedUpdateWithState.apply(LOGGER, (triggeringItem as GenericItem))
    ComputerRoom_IrBlaster_scenes_control.sendCommand(CONTROL_SCENE_CHROMECAST)
    //triggeringItem.postUpdate(OnOffType::OFF)
end
rule "Computer room scene, cloud item update, computer"
when
    Item Cloud_Scene_GF_ComputerRoom_computer received update ON
then
    val String LOGGER = LOGGER_NAME + ".sceneComputer"
    logReceivedUpdateWithState.apply(LOGGER, (triggeringItem as GenericItem))
    ComputerRoom_IrBlaster_scenes_control.sendCommand(CONTROL_SCENE_COMPUTER)
    //triggeringItem.postUpdate(OnOffType::OFF)
end
rule "Computer room scene, cloud item update, all off"
when
    Item Cloud_Scene_GF_ComputerRoom_allOff received update ON
then
    val String LOGGER = LOGGER_NAME + ".sceneAllOff"
    logReceivedUpdateWithState.apply(LOGGER, (triggeringItem as GenericItem))
    ComputerRoom_IrBlaster_scenes_control.sendCommand(CONTROL_SCENE_ALLOFF)
    //triggeringItem.postUpdate(OnOffType::OFF)
end

// When the Computer room IR blaster scenes control element receives update
//  Issue associated commands
//  Reset UI element to non-pressed state
rule "Computer room IR, scenes control command"
when
    Item ComputerRoom_IrBlaster_scenes_control received update
then
    val String LOGGER = LOGGER_NAME + ".sceneCtl"
    logReceivedUpdateWithState.apply(LOGGER, (triggeringItem as GenericItem))

    val GenericItem IrBlasterItem                   = IR_GF_ComputerRoom_Broadlink_RM3Mini_command
    val StringItem  AudioControlItem                = ComputerRoom_IrBlaster_Audio_control
    val SwitchItem  TvBacklightDimDownTriggerItem   = ComputerRoom_TvBacklight_DimDown_trigger
    val SwitchItem  TvBacklightDimUpTriggerItem     = ComputerRoom_TvBacklight_DimUp_trigger

    val NumberItem  ControlItem = (triggeringItem as NumberItem)
    val int receivedCommand = (ControlItem.state as DecimalType).intValue
    logInfo(LOGGER, "Scenes control received command code {}.", receivedCommand)
    if( receivedCommand != CONTROL_SCENE_NOCHANGE ) {
        switch receivedCommand {
            case CONTROL_SCENE_ALLOFF: {
                logInfo(LOGGER, "Received all off scene command")

                // Dim down tv backlight
                TvBacklightDimDownTriggerItem.postUpdate(OnOffType::ON)

                // Turn off tv
                transmitIrCommand.apply(LOGGER, "CONTROL_TV_POWER_OFF", g_computerRoomLockIrCommands, g_tvIrCommandsDefMap, IrBlasterItem)

                // Turn off receiver
                transmitIrCommand.apply(LOGGER, "CONTROL_AUDIO_VOL_MINUS_30", g_computerRoomLockIrCommands, g_audioIrCommandsDefMap, IrBlasterItem)
                transmitIrCommand.apply(LOGGER, "CONTROL_AUDIO_POWER_TOGGLE", g_computerRoomLockIrCommands, g_audioIrCommandsDefMap, IrBlasterItem)
                AudioControlItem.sendCommand("CONTROL_AUDIO_POWER_OFF")

            }
            case CONTROL_SCENE_CHROMECAST,
            case CONTROL_SCENE_COMPUTER,
            case CONTROL_SCENE_NINTENDO: {
                var String tvInputCommand
                var String audioInputCommand
                var String audioVolumeCommand
                if(receivedCommand == CONTROL_SCENE_CHROMECAST) {
                    logInfo(LOGGER, "Received chromecast scene command")
                    tvInputCommand      = "CONTROL_TV_INPUT_HDMI3"
                    audioInputCommand   = "CONTROL_AUDIO_INPUT_OPTICAL"
                    audioVolumeCommand  = "CONTROL_AUDIO_VOL_DEF_CC"
                }
                else if(receivedCommand == CONTROL_SCENE_COMPUTER) {
                    logInfo(LOGGER, "Received computer scene command")
                    tvInputCommand      = "CONTROL_TV_INPUT_HDMI1"
                    audioInputCommand   = "CONTROL_AUDIO_INPUT_DIRECT"
                    audioVolumeCommand  = "CONTROL_AUDIO_VOL_DEF_COMPUTER"
                }
                else if(receivedCommand == CONTROL_SCENE_NINTENDO) {
                    logInfo(LOGGER, "Received Nintendo scene command")
                    tvInputCommand      = "CONTROL_TV_INPUT_HDMI4"
                    audioInputCommand   = "CONTROL_AUDIO_INPUT_OPTICAL"
                    audioVolumeCommand  = "CONTROL_AUDIO_VOL_DEF_CC"
                }
                else {
                    logWarn(LOGGER, "Unable to handle scene command: {}", receivedCommand)
                }

                // In case recveiver is on, turn volume down
                transmitIrCommand.apply(LOGGER, "CONTROL_AUDIO_VOL_MINUS_30", g_computerRoomLockIrCommands, g_audioIrCommandsDefMap, IrBlasterItem)

                // Dim up tv backlight
                TvBacklightDimUpTriggerItem.postUpdate(OnOffType::ON)

                // Tv
                transmitIrCommand.apply(LOGGER, "CONTROL_TV_DIGIT_1", g_computerRoomLockIrCommands, g_tvIrCommandsDefMap, IrBlasterItem)
                Thread::sleep(5000) // extra wait in case tv was powered down

                transmitIrCommand.apply(LOGGER, "CONTROL_TV_VOL_MUTE", g_computerRoomLockIrCommands, g_tvIrCommandsDefMap, IrBlasterItem)
                transmitIrCommand.apply(LOGGER, "CONTROL_TV_VOL_MINUS_40", g_computerRoomLockIrCommands, g_tvIrCommandsDefMap, IrBlasterItem)
                transmitIrCommand.apply(LOGGER, "CONTROL_TV_VOL_PLUS", g_computerRoomLockIrCommands, g_tvIrCommandsDefMap, IrBlasterItem)
                transmitIrCommand.apply(LOGGER, "CONTROL_TV_VOL_MINUS", g_computerRoomLockIrCommands, g_tvIrCommandsDefMap, IrBlasterItem)
                transmitIrCommand.apply(LOGGER, tvInputCommand, g_computerRoomLockIrCommands, g_tvIrCommandsDefMap, IrBlasterItem)

                // Receiver
                AudioControlItem.sendCommand("CONTROL_AUDIO_POWER_ON")
                Thread::sleep(2000) // wait for power on sequence to get started, takes up to 8500 to complete
                //logInfo(LOGGER,"Start wait for power on")
                try {
                    // Wait until power on sequence completes
                    g_computerRoomLockReceiverUpdates.tryLock(20,TimeUnit.SECONDS)
                    //logInfo(LOGGER,"Power on lock aquired")
                } catch(Throwable t) {
                    //logInfo(LOGGER,"Wait for power on failed")
                    logError(LOGGER, "Exception caught when attempting to wait for receiver power toggle. Exception was: {}", t.toString())
                }
                finally {
                    g_computerRoomLockReceiverUpdates.unlock()
                    //logInfo(LOGGER,"Power on lock released")
                }
                //logInfo(LOGGER,"Done wait for power on")

                transmitIrCommand.apply(LOGGER, "CONTROL_AUDIO_INPUT_COAX", g_computerRoomLockIrCommands, g_audioIrCommandsDefMap, IrBlasterItem)
                transmitIrCommand.apply(LOGGER, "CONTROL_AUDIO_VOL_MINUS_30", g_computerRoomLockIrCommands, g_audioIrCommandsDefMap, IrBlasterItem)
                transmitIrCommand.apply(LOGGER, audioInputCommand, g_computerRoomLockIrCommands, g_audioIrCommandsDefMap, IrBlasterItem)
                transmitIrCommand.apply(LOGGER, audioVolumeCommand, g_computerRoomLockIrCommands, g_audioIrCommandsDefMap, IrBlasterItem)
            }
            default:{
                logWarn(LOGGER, "Unexpected scene command received: {}", receivedCommand)
            }
        }

        // To prevent the UI showing button in "pressed" state
        ControlItem.postUpdate(CONTROL_SCENE_NOCHANGE)
    }
end

// When computer room TV Ir blaster control element receives update
//  Issue associated IR command
//  Reset UI element to non-pressed state
rule "Computer room IR, TV control command"
when
    Item ComputerRoom_IrBlaster_TV_control received update
then
    val String LOGGER = LOGGER_NAME + ".tvCtl"
    logReceivedUpdateWithState.apply(LOGGER, (triggeringItem as GenericItem))

    val GenericItem IrBlasterItem = IR_GF_ComputerRoom_Broadlink_RM3Mini_command
    val HashMap<String, HashMap<String,String>> irCommandsMap = new HashMap<String, HashMap<String,String>>(g_tvIrCommandsDefMap)

    val StringItem ControlItem = (triggeringItem as StringItem)
    val String receivedCommand = ControlItem.state.toString()
    logDebug(LOGGER, "TV control received command code {}.", receivedCommand)
    if( receivedCommand != CONTROL_NOCHANGE ) {

        g_computerRoomLockTvUpdates.lock()
        try {
            transmitIrCommand.apply(LOGGER, receivedCommand, g_computerRoomLockIrCommands, irCommandsMap, IrBlasterItem)
        } catch(Throwable t) {
            logError(LOGGER, "Exception caught when handling control command. The exception was: {}", t.toString())
        } finally {
            g_computerRoomLockTvUpdates.unlock()
        }

        // To prevent the UI showing button in "pressed" state
        ControlItem.postUpdate(CONTROL_NOCHANGE)
    }
end

// When computer room Audio Ir blaster control element receives update
//  Issue associated IR command
//  Reset UI element to non-pressed state
rule "Computer room IR, Audio control command"
when
    Item ComputerRoom_IrBlaster_Audio_control received update
then
    val String LOGGER = LOGGER_NAME + ".audioCtl"
    logReceivedUpdateWithState.apply(LOGGER, (triggeringItem as GenericItem))

    val GenericItem IrBlasterItem = IR_GF_ComputerRoom_Broadlink_RM3Mini_command
    val HashMap<String, HashMap<String,String>> irCommandsMap = new HashMap<String, HashMap<String,String>>(g_audioIrCommandsDefMap)
    val SwitchItem ReceiverWallplugItem = Wallplug_GF_ComputerRoom_ReceiverPower_switch

    val StringItem ControlItem = (triggeringItem as StringItem)
    val String receivedCommand = ControlItem.state.toString()
    if( receivedCommand != CONTROL_NOCHANGE ) {
        // Some commands are not discrete IR commands
        switch receivedCommand {
            case CONTROL_AUDIO_POWER_ON: {
                logDebug(LOGGER, "Audio control received power ON command.")

                g_computerRoomLockReceiverUpdates.lock()
                try {
                    // Wallplug: Toggle power receiver
                    ReceiverWallplugItem.sendCommand(OnOffType::OFF)
                    Thread::sleep(2500)
                    ReceiverWallplugItem.sendCommand(OnOffType::ON)
                    Thread::sleep(7000)

                    // Turn on receiver by ir
                    transmitIrCommand.apply(LOGGER, "CONTROL_AUDIO_POWER_TOGGLE", g_computerRoomLockIrCommands, irCommandsMap, IrBlasterItem)

                } catch(Throwable t) {
                    logError(LOGGER, "Exception caught when attempting enable receiver power. Exception was: {}", t.toString())
                } finally {
                    g_computerRoomLockReceiverUpdates.unlock()
                }

            }
            case CONTROL_AUDIO_POWER_OFF: {
                logDebug(LOGGER, "Audio control received power OFF command.")
                g_computerRoomLockReceiverUpdates.lock()
                try {
                    // Wallplug: Toggle power receiver
                    ReceiverWallplugItem.sendCommand(OnOffType::OFF)
                    Thread::sleep(2500)
                    ReceiverWallplugItem.sendCommand(OnOffType::ON)
                    Thread::sleep(1000)
                } catch(Throwable t) {
                    logError(LOGGER, "Exception caught when attempting to toggle receiver power. Exception was: {}", t.toString())
                } finally {
                    g_computerRoomLockReceiverUpdates.unlock()
                }
            }
            default: {
                logDebug(LOGGER, "Audio control received IR command {}.", receivedCommand)
                g_computerRoomLockReceiverUpdates.lock()
                try {
                    transmitIrCommand.apply(LOGGER, receivedCommand, g_computerRoomLockIrCommands, irCommandsMap, IrBlasterItem)
                } catch(Throwable t) {
                    logError(LOGGER, "Exception caught when handling control command. The exception was: {}", t.toString())
                } finally {
                    g_computerRoomLockReceiverUpdates.unlock()
                }
            }
        }

        // To prevent the UI showing button in "pressed" state
        ControlItem.postUpdate(CONTROL_NOCHANGE)
    }
end

